apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'org.jetbrains.kotlin.android.extensions'

apply plugin: 'io.fabric'

androidExtensions {
    experimental = true
}

apply from: 'scripts.gradle'

android {
    compileSdkVersion Version.compile_sdk
    defaultConfig {
        applicationId "com.bariski.cryptoniffler"
        minSdkVersion Version.min_sdk
        targetSdkVersion Version.target_sdk
        versionCode Version.version_code
        versionName Version.version_name
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            aaptOptions.cruncherEnabled = false
            applicationIdSuffix ".dev"
            buildConfigField "String", "BUILD_PREFIX", "\"dev_\""
            resValue "string", "app_version", "v${defaultConfig.versionName}"

        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            buildConfigField "String", "BUILD_PREFIX", "\"\""
            resValue "string", "app_version", "v${defaultConfig.versionName}"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError false
        lintConfig file("$projectDir/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/MANIFEST.MF'
    }
    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        unitTests {
            returnDefaultValues = true
        }
    }
    sourceSets {
        debug {
            java.srcDirs += ["src/common/java"]
        }

        release {
            java.srcDirs += ["src/common/java"]
        }
    }
}
apply from: 'googleservices.gradle'

configurations.all {
    resolutionStrategy {
        force 'com.squareup.okio:okio:1.11.0'
        force "com.squareup.okhttp3:okhttp:3.5.0"
        force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Deps.kotlin
    implementation Deps.support
    implementation Deps.rx
    implementation Deps.firebase
    implementation Deps.network
    implementation Deps.custom_ui
    implementation Deps.logging
    implementation Deps.storage
    implementation Deps.dagger
    debugImplementation Deps.leakcanary_debug
    releaseImplementation Deps.leakcanary_release
    testImplementation Deps.junit
    androidTestImplementation Deps.android_test_runner
    androidTestImplementation Deps.espresso

    kapt  Deps.annotation_procs
}


apply plugin: 'com.google.gms.google-services'
